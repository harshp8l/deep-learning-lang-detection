(ns hello-world.core
  (:import [java.nio.file Files Paths SimpleFileVisitor StandardCopyOption FileVisitResult LinkOption CopyOption]
           [java.nio.file.attribute FileAttribute]))

(defn path [str-path]
  (Paths/get str-path (into-array String [])))

(defn copy-dir-visitor [from to]
  (proxy [SimpleFileVisitor] []
    (preVisitDirectory [dir attrs]
      (let [target (.resolve to (.relativize from dir))]
        (if-not (Files/exists target (into-array LinkOption []))
          (Files/createDirectory target (into-array FileAttribute [])))
        FileVisitResult/CONTINUE))
    (visitFile [file attrs]
      (let [target (.resolve to (.relativize from file))]
        (Files/copy file target (into-array CopyOption [StandardCopyOption/REPLACE_EXISTING])))
      FileVisitResult/CONTINUE)))

(defn copy-dir! [from to]
  (let [visitor (copy-dir-visitor from to)]
    (Files/walkFileTree from visitor)))
