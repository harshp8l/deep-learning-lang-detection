@setlocal ENABLEEXTENSIONS
@rem @set prompt=$G

@set _volume=Main Drive
@set _drives=Z: Y: X: W: V: U: T: S: R: Q: P: O: N: M: L: K: J: I: H: G: F: E: D: C: B: A:
@set _driveFound=

@for %%G in (%_drives%) do @call :FindVolumeName %%G "%_volume%"
@if defined _driveFound (@goto :_InstallIt)
@echo Volume named %_volume% not found.
@exit /b 0

:FindVolumeName
@set _driveLetter=%1
@set _driveLetter=%_driveLetter:~0,1%
@if not exist %1\ @exit /b 0
@vol %1 | @findstr /i /c:"Volume in drive %_driveLetter% is %~2" > NUL
@if %ERRORLEVEL% equ 0 @set _driveFound=%1
@exit /b 0

:_InstallIt
@set _dest=%_driveFound%\Temp\
@echo Installing to %_dest% ...
@rem Remove the @echo from the following line when you are sure source and dest paths are correct!
@echo @robocopy InstallApp\* %_dest% /MIR
@pause
@exit /b 0
