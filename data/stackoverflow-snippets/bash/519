awk '
FNR==NR{                                ##FNR and NR both are awk out of the box variables and they denote line numbers in Input_file(s), difference between them is FNR value will be RESET when it complete reading 1 Input_file and NR value will be keep increasing till it completes reading all the Input_file(s).
  a[$NF]=$0;                            ##Creating an array named a whose index is $NF(value of last field of current line) and value is current line.
  next                                  ##next is awk out of the box keyword which will skip all further statements now.
}
$1~/:/{                                 ##Checking condition here if current lines 1st field has a colon in it then do following:
  sub(/:/,"",$1);                       ##Using sub function of awk which will substitute colon with NULL of 1st field of current line of current Input_file.
  flag=1                                ##Setting a variable named flag here(basically to make sure that 1st colon is substituted so need for another colon removal.
}
($1 in a){                              ##Checking a condition here if current line $1 is present in array a then do following:
  val=$1;                               ##Setting variable named val value to $1 here.
  if($0 ~ /:/ && !flag){                ##Checking condition here if current line is having colon and variable flag is NOT NULL then do following:
     sub(/[^:]*/,"");                   ##Substituting all the values from starting to till colon comes with NULL.
     sub(/:/,"")};                      ##Then substituting only 1 colon here.
  print a[val] OFS $0 > "result.txt";   ##printing the value of array a whose index is variable val OFS(output field separator) current line values to output file named results.txt here.
  flag="";                              ##Unsetting the value of variable flag here.
  delete a[val]                         ##Deleting the value of array a whose index is variable val here.
}
END{                                    ##Starting end section of this awk program here. which will be executed once all Input_file(s) have been read.
  for(i in a){                          ##Traversing through the array a now.
     print a[i]>"left.txt"}             ##Printing the value of array a(which will basically provide those values which are NOT matched in both files) in left.txt file.
}
' FS="|" 1.txt FS=" " OFS="||o||" 2.txt ##Setting FS="|" for 1.txt Input_file and then setting FS=" " and OFS="||o||" for 2.txt Input_file, 1.txt and 2.txt are Input_files for this program to run.
