$ cat a.awk
function print_term(i) {
    # Don't print zero terms:                                                                                                                                                 
    if (!$i) return;

    # Print a "+" unless this is the first term:                                                                                                                               
    if (!first) { printf " + " }

    # If it's the last term, just print the number:                                                                                                                            
    if (i == NF) printf "%d", $i

    # Leave the coefficient blank if it's 1:                                                                                                                                    
    coef = ($i == 1 ? "" : $i)

    # If it's the penultimate term, just print an 'x' (not x^1):                                                                                                               
    if (i == NF-1) printf "%sx", coef

    # Print a higher-order term:
    if (i < NF-1) printf "%sx^%s", coef, NF - i
    first = 0
}

{
  first = 1

  # print all the terms:
  for (i=1; i<=NF; ++i) print_term(i)

  # If we never printed any terms, print a "0":
  print first ? 0 : ""
}
