 IF OBJECT_ID('tempdb..#Sample') IS NOT NULL
    DROP TABLE #Sample

create table #Sample (
  Id Int,
  DOB Varchar (50))

insert into #Sample(Id, DOB)
Values 
(38603, '24/02/1969'),
(38605, '22/09/1969'),
(36356, '17/03/1954'),
(36374, '17/05/1975'),
(36441, '17/08/1961'),
(119, '10/05/9999'),
(114, '10/99/9999'),
(132, '99/99/9999'),
(25125, '--/--/1935'),
(2323, '00/00/1935'),
(2512, '88/88/1935'),
(2156, '99/99/1935'),
(368, '10/--/1935'),
(34135, '10/00/1935'),
(3435, '10/88/1935'),
(3241, '10/99/1935'),
(4512, '--/09/1935'),
(4161, '00/09/1935'),
(4312, '88/09/1935'),
(456, '99/09/1935')

;WITH ParsedBirth AS
(
    SELECT
        S.Id,
        S.DOB,
        Year = SUBSTRING(S.DOB, 7, 4),
        Month = SUBSTRING(S.DOB, 4, 2),
        Day = SUBSTRING(S.DOB, 1, 2)
    FROM
        #Sample AS S
),
ParsedBirthInteger AS
(
    SELECT
        P.Id,
        P.DOB,
        Year = CASE WHEN ISNUMERIC(P.Year) = 1 AND P.Year <> '9999' THEN CONVERT(INT, P.Year) END,
        Month = CASE 
            WHEN ISNUMERIC(P.Month) = 1 AND CONVERT(INT, P.Month) BETWEEN 1 AND 12 THEN CONVERT(INT, P.Month) 
            ELSE 7 END,
        Day = CASE 
            WHEN ISNUMERIC(P.Day) = 1 AND CONVERT(INT, P.Day) BETWEEN 1 AND 31 THEN CONVERT(INT, P.Day) 
            ELSE 15 END
    FROM
        ParsedBirth AS P
),
InferredBirth AS
(
    SELECT
        P.Id,
        P.DOB,
        InferredBirth = CONVERT(DATE, CONVERT(VARCHAR(100), P.Year * 10000 + P.Month * 100 + P.Day))
    FROM
        ParsedBirthInteger AS P
)
SELECT
    T.Id,
    T.DOB,
    T.InferredBirth,
    Age = (CONVERT(INT,CONVERT(char(8), GETDATE(),112))-CONVERT(char(8),T.InferredBirth,112))/10000
FROM
    InferredBirth AS T
