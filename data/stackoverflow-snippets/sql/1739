CREATE FUNCTION dbo.SplitCommentOnTime(@str VARCHAR(MAX))
RETURNS TABLE
AS
RETURN
WITH recCTE AS
(
    SELECT 1 AS Pos
          ,LEFT(@str,PATINDEX('%:[0-5][0-9] [AP]M %',@str + ' ')+5) AS Part
          ,SUBSTRING(@str,PATINDEX('%:[0-5][0-9] [AP]M %',@str + ' ')+7,LEN(@str)) AS Remainder

    UNION ALL
    SELECT Pos+1
          ,LEFT(Remainder,PATINDEX('%:[0-5][0-9] [AP]M %',Remainder + ' ')+5) 
          ,SUBSTRING(Remainder,PATINDEX('%:[0-5][0-9] [AP]M %',Remainder + ' ')+7,LEN(@str)) 
    FROM recCTE
    WHERE PATINDEX('%:[0-5][0-9] [AP]M %',Remainder + ' ')>0
)
SELECT Pos,Part 
FROM recCTE;
GO

DECLARE @tbl TABLE(ID INT IDENTITY,Comment VARCHAR(MAX));
INSERT INTO @tbl VALUES
 ('CLAIM REFILED Rebecca Byrd 1/17/2018 3:17:53 PM PER CHGS, WE NEED TO REFILE THIS CLAIM Rebecca Byrd 1/10/2018 1:55:37 PM WAITING ON HOME PLAN TO REPLY Rebecca Byrd 1/2/2018 1:58:31 PM A/R SENT TO CHGS ON THIS CLAIM. DENIED AS A DUPLICATE, BUT THERE WAS ONLY ONE CLAIM IN ILINKBLUE FOR THIS DOS. Test Byrd 12/29/2017 6:34:36 AM')
,('HCRR ACCOUNT Sheila Johnson 9/28/2017 7:37:55 AM')
,('Contacted VA VISN spoke with Mary she stated pmt $18.32 was made on 06/27/2014 - #675678- she gave me treasury uh# 86-72-1141. I contacted TEST and spoke with TEST - i gave her number, date and pmt - she found ck and pmt of $0.00 for date 06/27/2014- she said it was cashed under blah bank - trace #0jgdjgkd. Test Test 7/28/2017 1:21:11 PM')

SELECT *
FROM @tbl 
OUTER APPLY dbo.SplitCommentOnTime(Comment); 


GO
DROP FUNCTION dbo.SplitCommentOnTime;
