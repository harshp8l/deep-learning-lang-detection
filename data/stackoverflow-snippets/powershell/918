@echo off
setlocal EnableExtensions DisableDelayedExpansion

rem // Define constants here:
set "_GROUP=GroupName" & rem // (define the group name here)
set "_SPACES=                " & rem // (16 spaces)

rem // Initialise variables:
set "LINE=" & set "FLAG="
rem // Loop through all (non-empty) output lines of `net group`:
for /F delims^=^ eol^= %%L in ('net group "%_GROUP%" /DOMAIN') do (
    rem // Toggle delayed expansion to avoid loss of or trouble with `!`:
    setlocal EnableDelayedExpansion
    rem // Query state of flag:
    if defined FLAG (
        rem // Split the line string into three 25-character strings:
        for /L %%P in (0,25,50) do (
            rem // Extract a single column item (a 25-character string):
            set "COL=!LINE:~%%P,25!"
            rem // Split off trailing space characters:
            for %%S in (16 8 4 2 1) do (
                if defined COL (
                    if "!COL:~-%%S!"=="!_SPACES:~,%%S!" (
                        set "COL=!COL:~,-%%S!"
                    )
                )
            )
            rem // Return non-empty right-trimmed column item:
            if defined COL echo(!COL!
        )
    )
    rem // Set flag in case last header line is encountered (`-` only):
    if "!LINE:-=!"=="" (endlocal & set "FLAG=#") else endlocal
    rem /* Store currently read line to be processed in the next loop iteration;
    rem    this lets the last line be ignored, which is just a success message: */
    set "LINE=%%L"
)

endlocal
exit /B
