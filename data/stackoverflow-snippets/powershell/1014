@Echo off

REM Get Date and Time
for /f "tokens=2 delims==" %%a in ('wmic OS Get localdatetime /value') do set "dt=%%a"
set yyyy=%dt:~0,4%
set mm=%dt:~4,2%
set dd=%dt:~6,2%

REM Get Last Day of month
call :DaysOfMonth %yyyy% %mm%

IF "%lastday%"=="%dd%" (
    shutdown /r /t 86400
) else (
    shutdown /r /t 0
)
GOTO :EOF

:DaysOfMonth Year Month
setlocal DisableDelayedExpansion
set /a "yy = %~1, mm = 100%~2 %% 100"
set /a "n = 30 + !(((mm & 9) + 6) %% 7) + !(mm ^ 2) * (!(yy %% 4) - !(yy %% 100) + !(yy %% 400) - 2)"
endlocal &set lastday=%n%
GOTO :EOF
